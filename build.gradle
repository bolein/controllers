// Top-level build file where you can add configuration options common to all sub-projects/modules.


buildscript {
    repositories {
        jcenter()
        google()
    }
    ext {
        supportVersion = '27.0.2'
        toolsVersion = '27.0.3'
        compileVersion = 27
        targetVersion = 27
        minVersion = 16
        kotlin_version = '1.2.41'
        kotlin_coroutines_version = '0.22.5'
        gradlePluginVer = '3.1.2'
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVer"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'

// build a jar with source files
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        failOnError  false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }

// build a jar with javadoc
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

//     uncomment to build a jar file in addition to the default aar file
    android.libraryVariants.all { variant ->
        def name = variant.buildType.name
        def task = project.tasks.create "jar${name.capitalize()}", Jar
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        artifacts.add('archives', task);
    }

}
